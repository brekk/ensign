import type { Maybe } from "Maybe"

import { ifElse, when } from "Function"
import IO from "IO"
import List from "List"
import { Just, Nothing } from "Maybe"
import String from "String"

import O from "Orthography"

import { log } from "@/Log"



export type Flag = FlagToggle(String, Boolean) | Flag(String, String)

// converter = O.translate(O.bidi(O.pairs.kebab, O.pairs.camel))
// format = converter(true)

export on = FlagToggle($, true)
export off = FlagToggle($, false)

isStringFlag :: String -> Boolean
export isStringFlag = String.startsWith("--")

isNegatedFlag :: String -> Boolean
export isNegatedFlag = String.startsWith("--no-")

isShortFlag :: String -> Boolean
export isShortFlag = String.startsWith("-")

isFlag :: String -> Boolean
export isFlag = (x) => isShortFlag(x) || isStringFlag(x) || isNegatedFlag(x)

flagSlug :: String -> String
export flagSlug = when(
  isFlag,
  ifElse(isNegatedFlag, String.drop(5), ifElse(isStringFlag, String.drop(2), String.drop(1))),
)

_splitJointFlags :: String -> List String
export _splitJointFlags = (x) => if (String.includes('=', x)) {
  String.split("=", x)
} else if (isShortFlag(x) && !isStringFlag(x)) {
  pipe(
    String.drop(1),
    String.split(""),
    map(mappend("-")),
  )(x)
} else {
  of(x)
}

_handleMultipleBooleanFlags :: List Flag -> List String -> List Flag
export _handleMultipleBooleanFlags = (stack, x) => pipe(
  map(
    pipe(
      String.drop(1),
      FlagToggle($, true),
    ),
  ),
  List.concat(stack),
)(x)

_handleStringFlag :: List Flag -> List String -> String -> List Flag
export _handleStringFlag = (stack, curr, fixed) => where(List.nth(0, curr)) {
  Just(firstCurr) =>
    if (!isFlag(firstCurr)) {
      List.append(Flag(fixed, firstCurr), stack)
    } else {
      stack
    }

  _ =>
    stack
}

_handleBooleanFlag :: List Flag -> String -> String -> List Flag
export _handleBooleanFlag = (stack, prev, fixed) => if (!isFlag(prev)) {
  stack
} else {
  List.append(FlagToggle(fixed, !isNegatedFlag(prev)), stack)
}

_handleOtherFlags :: List Flag -> String -> List String -> List Flag
export _handleOtherFlags = (stack, prev, current) => {
  fixed = flagSlug(prev)
  currentIsSingle = List.length(current) == 1
  return if (isStringFlag(prev) || isShortFlag(prev) && currentIsSingle) do {
    log.detailStack(IO.green("isStringFlag"), isStringFlag(prev))
    return if (isNegatedFlag(prev)) {
      List.append(FlagToggle(fixed, false), stack)
    } else if (currentIsSingle) {
      _handleStringFlag(stack, current, fixed)
    } else {
      pipe(
        (f) => {
          log.detailStack("\tskipping?!?!?", { stack, prev, fixed: f })
          return f
        },
        _handleBooleanFlag(stack, prev),
      )(fixed)
    }
  } else {
    pipe(
      (f) => {
        log.detailStack("\thandling boolean", { stack, prev, fixed: f })
        return f
      },
      _handleBooleanFlag(stack, prev),
    )(fixed)
  }
}

_updateStack :: List Flag -> Maybe (List String) -> List String -> List Flag
export _updateStack = (stack, maybePrevious, current) => {
  log.detailStack("▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚", stack)
  return where(maybePrevious) {
    Just(previous) =>
      do {
        prevLen = List.length(previous)
        return if (prevLen > 2) {
          _handleMultipleBooleanFlags(stack, previous)
        } else {
          where(previous) {
            [k, v] =>
              isFlag(k) && !isFlag(v)
                ? List.append(Flag(flagSlug(k), v), stack)
                : _handleMultipleBooleanFlags(stack, previous)

            [x] =>
              _handleOtherFlags(stack, x, current)

            _ =>
              log.detailStack("\tUNHANDLED CASE!?!?!?", stack)
          }
        }
      }

    Nothing =>
      log.detailStack("\tNOTHING CASE!?!?!?", stack)
  }
}

parseFlags :: List String -> List Flag
export parseFlags = pipe(
  map(_splitJointFlags),
  (steps) => List.reduce(
    (agg, curr) => ({ prev: Just(curr), stack: _updateStack(agg.stack, agg.prev, curr) }),
    { stack: [], prev: List.nth(0, steps) },
    // stuff an empty value at the end so we process all entries
    List.append([], List.tail(steps)),
  ),
  .stack,
)
